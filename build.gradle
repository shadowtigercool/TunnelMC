plugins {
	id 'fabric-loom' version '1.1-SNAPSHOT'
	id 'maven-publish'
	id 'com.github.johnrengelman.shadow' version '8.1.1'
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

configurations {
	// include libraries
	implementation.extendsFrom(library)
	shadow.extendsFrom(library)
}

repositories {
    maven {
        name = 'opencollab-releases'
        url = 'https://repo.opencollab.dev/maven-releases/'
    }
    maven {
        name = 'opencollab-snapshots'
        url = 'https://repo.opencollab.dev/maven-snapshots/'
    }
}

dependencies {
	// Minecraft-related
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	// Bedrock protocol & Live Authentication
	library("com.nukkitx.protocol:bedrock-v560:2.9.15-SNAPSHOT") {
		exclude group: 'com.nukkitx.fastutil'
	}
	library "com.github.scribejava:scribejava-apis:8.3.3"

	// Fabric API and adventure. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	modImplementation include("net.kyori:adventure-platform-fabric:5.7.0")
	implementation "net.kyori:adventure-text-serializer-legacy:4.13.0"

	// Lombok
	compileOnly 'org.projectlombok:lombok:1.18.26'
	annotationProcessor 'org.projectlombok:lombok:1.18.26'

	// Jackson
	library "com.fasterxml.jackson.core:jackson-databind:2.14.2"
	library("com.nukkitx.jackson.dataformat:jackson-dataformat-nbt:2.12.4-SNAPSHOT") {
		exclude group: 'com.nukkitx.nbt'
	}

	// Events
	library("com.nukkitx:common:2.6.2-SNAPSHOT") {
		exclude group: 'com.google.guava'
	}
}

shadowJar {
	configurations = [ project.configurations.shadow ]
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	// Minecraft 1.19.3 (1.1.9.3) upwards uses Java 17.
	it.options.release = 17
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			// add all the jars that should be included when publishing to maven
			artifact(remapJar) {
				builtBy remapJar
			}
		}
	}

	// select the repositories you want to publish to
	repositories {
		// uncomment to publish to the local maven
		// mavenLocal()
	}
}

remapJar {
	dependsOn shadowJar
	inputFile.set(shadowJar.archiveFile)
}
